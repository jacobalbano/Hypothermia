package com.thaumaturgistgames.flakit
{
	import flash.display.MovieClip;
	import flash.events.Event;
	import com.thaumaturgistgames.utils.Input;
	import flash.events.KeyboardEvent;
	import flash.ui.Keyboard;
	
	/**
	 * FLAKit bootloader
	 * @author Jake Albano
	 */
	
	public class Engine extends MovieClip
	{
		private var flags:Boolean;
		private var isInitialized:Boolean;
		private var resourceClass:Class;
		public var console:Console;
		public static var game:Game;
		public static var engine:Engine;
		public var onReload:Function;
		
		/**
		 * Creates a new engine base
		 * @param	flags	Behavior flags for the Library class
		 * @param	resourceClass	Optionally, you can pass in a class type containing embedded assets, like the one generated by
		 * the EmbeddedLibraryBuilder tool. In order for this to work, the flag must be set to Library.USE_EMBEDDED. Not compatible
		 * with Flash Professional unless the Flex SDK is installed
		 */
		public function Engine(flags:Boolean = false, resourceClass:Class = null)
		{
			addEventListener(Event.ADDED_TO_STAGE, added);
			this.flags = flags;
			this.resourceClass = resourceClass;
			onReload = null;
		}
		
		private function added(e:Event):void 
		{
			removeEventListener(Event.ADDED_TO_STAGE, added);
			
			
			isInitialized = false;
			
			game = (this as Game);
			engine = this;
			
			this.flags = flags;
			console = new Console();
			addChild(console);
			
			console.slang.addFunction("reload", reloadLibrary, [], this, "Reloads library assets");
			
			addEventListener(KeyboardEvent.KEY_UP, refresh);
			
			if ((this.flags == Library.USE_EMBEDDED) && resourceClass)
			{
				Library.init(this, flags);
				
				new resourceClass;
				
				Input.init(this);
				
				init();
			}
			else
			{
				if (this.flags == Library.USE_XML)
				{
					if (stage) load();
					else addEventListener(Event.ADDED_TO_STAGE, load);
				}
				else
				{
					init();
				}
			}
			
		}
		
		private function refresh(event:KeyboardEvent):void
		{
			if (event.keyCode == Keyboard.F5)
			{
				console.slang.doLine("reload");
			}
		}
		
		private function reloadLibrary():void 
		{
			if (flags == Library.USE_EMBEDDED)
			{
				console.print("Live reloading isn't supported for embedded assets!");
			}
			else
			{
				Library.init(this, flags);
			}
		}
		
		private function load(e:Event = null):void 
		{
			removeEventListener(Event.ADDED_TO_STAGE, load);
			this.addEventListener("libraryLoaded", loaded);
			
			Library.init(this, flags);
			
			Input.init(this);
		}
		
		private function loaded(event:Event):void
		{
			console.print("Library loaded:", Library.totalImages, Library.totalImages == 1 ? "image," : "images,", Library.totalSounds, Library.totalSounds == 1 ? "sound," : "sounds,", Library.totalXMLs, "xml", Library.totalXMLs == 1 ? "file" : "files");
			
			if (onReload != null)
			{
				onReload();
			}
			
			if (!isInitialized)
			{
				isInitialized = true;
				init();
			}
		}
		
		public function init():void
		{
			//	Entry point
		}
	}
	
}